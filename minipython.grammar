/*
* Dimitrios Tsiompikas 3180223
* Ana Lleshi 3180095
* Elena Fourtouni 3180196
* Petros Tsotsi 3180193
*/

Package minipython;

Helpers
	digit = ['0' .. '9'];
	letter = ['a' .. 'z']|['A' .. 'Z']; 
	cr = 13; 
	lf = 10;
	all = [0..127]; 
	eol = lf | cr | cr lf ;
	not_eol = [all - [cr + lf]]; 

Tokens
	tab = 9;
	plus = '+';
	minus = '-';
	mod = '%';
	mult = '*';
	dmult = '**';
	div = '/';
	eq = '=';
	eqminus = '-='; // diko mas 
	eqdiv = '/='; // diko mas
	exclam = '!'; 
	dictt = 'dict';
	def = 'def';
	logic_plus = '&&';
	l_par = '(';
	r_par = ')';
	l_br = '[';
	r_br = ']';
	comma=',';
	qmark = '?';
	gqmark = ';';
	dot = '.'; // diko mas
	if = 'if';
	elif = 'elif';
	else = 'else';
	while = 'while';
	for = 'for'; // diko mas
	in = 'in'; // diko mas
	print = 'print';
	return = 'return';
	assert = 'assert'; // diko mas
	len = 'len'; // diko mas
	max = 'max'; // diko mas
	min = 'min'; // diko mas
	import = 'import'; // diko mas 
	from = 'from'; // diko mas 
	as = 'as'; // diko mas 
	and = 'and'; // diko mas
	or = 'or'; // diko mas
	not = 'not'; // diko mas
	less = '<';
	great = '>';
	greateq = '>='; // diko mas
	lesseq = '<='; // diko mas
	diff = '!='; // diko mas
	doubleeq = '=='; // diko mas 
	true = 'true'; 
	semi = ':';
	false = 'false';
	none = 'None'; // diko mas
	quote = '"';
	blank = (' ' | lf | cr);
	line_comment = '#' not_eol* eol; 
	number = digit+ | (digit+ '.' digit+);
	id = letter (letter | digit)*;
	string = '"'not_eol* '"' | '''not_eol*''';

Ignored Tokens
	blank, line_comment;


Productions
	goal = commands*;

	commands = {import} import_rule |
			   {function} function |
			   {statement} statement;

	function = def id l_par argument? r_par semi statement;

	argument = param commaparam*;

	commaparam = comma param;

	param = id assign?;

	assign = eq value;

	statement = {if} tab* if complex_comparison semi statement |
				{while} tab* while complex_comparison semi statement |
				{for} tab* [id1]:id in [id2]:id semi statement |
				{return} tab* return really_complex_expression |
				{print} tab* print arglist |
				{assign} tab* id eq really_complex_expression |
				{assignmin} tab* id eqminus really_complex_expression |
				{assigndiv} tab* id eqdiv really_complex_expression |
				{brassign} tab* id l_br [exp1]:really_complex_expression r_br eq [exp2]:really_complex_expression |
				{assert} tab* assert really_complex_expression commaexpr? |
				{functioncall} tab* function_call;

	complex_comparison = {mediumcomp} medium_comparison |
									{not} not complex_comparison;

	medium_comparison = {simplecomp} simple_comparison |
						{and} medium_comparison and simple_comparison |
						{or} medium_comparison or simple_comparison;

	simple_comparison = {false} false |
					    {true} true |
					    {greater} [rce1]:really_complex_expression great [rce2]:really_complex_expression |
					    {less} [rce1]:really_complex_expression less [rce2]:really_complex_expression |
					    {greatereq} [rce1]:really_complex_expression greateq [rce2]:really_complex_expression |
					    {lesseq} [rce1]:really_complex_expression lesseq [rce2]:really_complex_expression |
					    {eq} [rce1]:really_complex_expression doubleeq [rce2]:really_complex_expression |
					    {diff} [rce1]:really_complex_expression diff [rce2]:really_complex_expression;

	function_call = id l_par arglist? r_par;

	arglist = really_complex_expression commaexpr*;

	commaexpr = comma really_complex_expression;

	really_complex_expression = {complexexpr} complex_expression |
							  {add} really_complex_expression plus complex_expression |
							  {sub} really_complex_expression minus complex_expression;

	complex_expression = {mediumexpr} medium_expression |
						 {mult} complex_expression mult medium_expression |
						 {div} complex_expression div medium_expression |
						 {mod} complex_expression mod medium_expression;

	medium_expression = {simpleexpr} simple_expression |
					    {power} medium_expression dmult simple_expression;

	simple_expression = {dummyexpr} dummy_expression |
						{brexpr} id l_br really_complex_expression r_br |
				 	    {functioncall} function_call |
				        {lenexpr} len l_par really_complex_expression r_par |
					    {brexprs} l_br arglist r_br;

	dummy_expression = {id} id |
					   {value} value |
					   {max} max l_par value commavalue* r_par |
					   {min} min l_par value commavalue* r_par;

	commavalue = comma value;

	value = {callonid} iddot function_call |
			{number} number |
			{string} string |
			{none} none;

	import_rule = {imp} import moduleas commamoduleas* |
				 {from} from module import idasid commaidasid*;

	commamoduleas = comma moduleas;

	moduleas = module asid?;

	module = iddot* id;
	
	commaidasid = comma idasid;

	idasid = id asid?;

	asid = as id;

	iddot = id dot;